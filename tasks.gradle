task copyGitHooks(type: Copy) {
  description 'Copies the git hooks from scripts/git-hooks to the .git folder.'
  from("${rootDir}/scripts/git-hooks") {
    include '**/*.sh'
    rename '(.*).sh', '$1'
  }
  into "${rootDir}/.git/hooks"
}

task installGitHooks(type: Exec) {
  description 'Installs the pre-commit git hooks from team-props/git-hooks.'
  group 'git hooks'
  workingDir rootDir
  commandLine 'chmod'
  args '-R', '+x', '.git/hooks/'
  dependsOn copyGitHooks
  onlyIf { isLinuxOrMacOs() }
  doLast {
    logger.info('Git hook installed successfully.')
  }
}

afterEvaluate {
  tasks['clean'].dependsOn installGitHooks
  tasks['assemble'].dependsOn installGitHooks
}

static def isLinuxOrMacOs() {
  def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
  return osName.contains('linux') || osName.contains('mac os') || osName.contains('macos')
}
